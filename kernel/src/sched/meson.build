sched_source_set = ssmod.source_set()

# schedulers implementations
sentry_sched_fifo_c = files(
    'sched_fifo.c',
)
sentry_sched_rrmq_c = files(
    'sched_rrmq.c',
)

sched_source_set.add(when: 'CONFIG_SCHED_FIFO', if_true: sentry_sched_fifo_c)
sched_source_set.add(when: 'CONFIG_SCHED_RRMQ', if_true: sentry_sched_rrmq_c)

sched_source_set_config = sched_source_set.apply(kconfig_data, strict: false)

sentry_sched_lib = static_library('sentry_sched',
    sources: [
        sched_source_set_config.sources(),
        sched_header_set_config.sources(),
    ],
    include_directories: kernel_inc,
    c_args: [ target_arch_args, global_build_args ],
    install: false,
    dependencies: [
        sentry_arch_dep,
        sentry_managers_dep,
        sched_source_set_config.dependencies(),
        external_deps
    ],
)

sentry_sched_dep = declare_dependency(
    link_with: sentry_sched_lib,
    sources: sched_header_set_config.sources(),
    include_directories: kernel_inc,
)
